group 'com.tiza'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    hadoopVersion = '2.5.2'
}

dependencies {
    compile fileTree(dir: rootDir.path + "/lib/op", include: "*.jar")

    compile group: 'org.apache.hadoop', name: 'hadoop-common', version: "$hadoopVersion",transitive: false
    compile group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-app', version: "$hadoopVersion",transitive: false
    compile group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: "$hadoopVersion",transitive: false
    compile group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-jobclient', version: "$hadoopVersion",transitive: false

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 拷贝文件
task copyJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/dist"
    exclude {
        //排除部署环境中存在的依赖
        it.file.name.contains('hadoop') || it.file.name.contains('tstar')
    }
}

task copyConf(type: Copy) {
    from "$projectDir/src/main/conf"
    into "$buildDir/libs/conf"
}

jar {
    archiveName = 'mstar_tiza.jar'
    from {
        File srcDir = file("$buildDir/dist")
        // 使用闭合创建一个文件集合
        FileCollection collection = files { srcDir.listFiles() }
        collection.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
jar.dependsOn(copyJar)

// 打包
task zip(type: Zip, dependsOn: [copyConf, jar]) {
    from 'build/libs'
}

// 设置编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}